-- (C) 2017 Wout Elsinghorst

optpragmas
{
{-# LANGUAGE PatternGuards #-}
}

imports
{
import CCO.Printing

import Data.Maybe
}

{
type Language = String
type Platform = String

type SourceAndTargetLanguage = (Language, Language)

data Diagnostic 
  = TyError SourcePos DiagType String 
  | Warning SourcePos String

type Implementation = Either Platform Language
  
data DiagType 
  = Type_Top 
  | Type_Program                          Implementation
  | Type_Compiler SourceAndTargetLanguage Implementation -- canCompileLanguage
  | Type_Executor Language                Implementation -- canExecuteLanguage

instance Show DiagType where
  show (Type_Top)                          = "Top"
  show (Type_Program           (Right m))  = "Program {"  ++ m ++  "}"
  show (Type_Program           (Left  m))  = "Program {!" ++ m ++ "!}"
  show (Type_Compiler (l1, l2) (Right m))  = "Compiler [" ++ l1 ++ " ~> " ++ l2 ++ "] {"  ++ m ++  "}"
  show (Type_Compiler (l1, l2) (Left  m))  = "Compiler [" ++ l1 ++ " ~> " ++ l2 ++ "] {!" ++ m ++ "!}" 
  show (Type_Executor l1       (Right l2)) = "Executor [" ++ l1 ++ " ~> " ++ l2 ++ "]"
  show (Type_Executor l1       (Left  _))  = "Executor {!" ++ l1 ++ "!}" 
}

attr Diag Diag_
  syn   diagType :: { DiagType }
  syn   typeDiagnostics :: { [Diagnostic] }
  syn   implementationLanguage :: { Either Platform Language }
  syn   canExecuteLanguage  :: { Maybe Language }
  syn   canCompileLanguage  :: { Maybe SourceAndTargetLanguage }
 
attr Diag_ 
  inh   pos :: { SourcePos }

sem Diag_
  | Program     lhs.diagType = mkProgramType  @implementationLanguage                    
  | Compiler    lhs.diagType = mkCompilerType @implementationLanguage @canCompileLanguage 
  
  | Platform    lhs.diagType = mkExecutorType @implementationLanguage @canExecuteLanguage  
  | Interpreter lhs.diagType = mkExecutorType @implementationLanguage @canExecuteLanguage  
      
  | Execute     lhs.diagType = replaceImplementationLanguage @implementationLanguage @d1.diagType
  | Compile     lhs.diagType = replaceImplementationLanguage @implementationLanguage @d1.diagType

  
sem Diag_
  | Program     lhs.typeDiagnostics = []
  | Compiler    lhs.typeDiagnostics = []
  
  | Platform    lhs.typeDiagnostics = []
  | Interpreter lhs.typeDiagnostics = []
      
  | Execute     lhs.typeDiagnostics = @d1.typeDiagnostics ++ @d2.typeDiagnostics ++ (checkTyExecute @lhs.pos @d1.diagType @d2.diagType)
  | Compile     lhs.typeDiagnostics = @d1.typeDiagnostics ++ @d2.typeDiagnostics ++ (checkTyCompile @lhs.pos @d1.diagType @d2.diagType)

sem Diag_
  | Program     loc.canExecuteLanguage = Nothing
  | Compiler    loc.canExecuteLanguage = Nothing
  
  | Platform    loc.canExecuteLanguage = Just @m
  | Interpreter loc.canExecuteLanguage = Just @l
      
  | Execute     loc.canExecuteLanguage = @d1.canExecuteLanguage
  | Compile     loc.canExecuteLanguage = @d1.canExecuteLanguage

sem Diag_
  | Program     loc.canCompileLanguage = Nothing
  | Compiler    loc.canCompileLanguage = Just (@l1, @l2)
  
  | Platform    loc.canCompileLanguage = Nothing
  | Interpreter loc.canCompileLanguage = Nothing
      
  | Execute     loc.canCompileLanguage = @d1.canCompileLanguage
  | Compile     loc.canCompileLanguage = @d1.canCompileLanguage
  
sem Diag_
  | Program     loc.implementationLanguage = Right @l
  | Compiler    loc.implementationLanguage = Right @m
  
  | Platform    loc.implementationLanguage = Left  @m
  | Interpreter loc.implementationLanguage = Right @m
      
  | Execute     loc.implementationLanguage = @d2.implementationLanguage
  | Compile     loc.implementationLanguage = case @d2.canCompileLanguage of
                                               Just (l1, l2) -> Right l2
                                               Nothing       -> Left "invalid"

sem Diag
  | Diag      lhs.typeDiagnostics        = @d.typeDiagnostics
  | Diag      lhs.implementationLanguage = @d.implementationLanguage
  | Diag      lhs.canExecuteLanguage     = @d.canExecuteLanguage
  | Diag      lhs.canCompileLanguage     = @d.canCompileLanguage

  | Diag      d.pos                      = @pos

{
checkTyExecute :: SourcePos -> DiagType -> DiagType -> [Diagnostic]
checkTyExecute pos d1 d2@(Type_Executor l2 _) | Just l1 <- getImplementationLanguage d1, l1 == l2  = [] -- Languages match!!
checkTyExecute pos d1 d2@(Type_Executor l2 _) | Just l1 <- getImplementationLanguage d1, l1 /= l2  = [TyError pos d1 $ "Execute: The implementation language '" ++ l1 ++ "' of the first diagram doesn't match the execution language '" ++ l2 ++ "' of the second diagram." ]
checkTyExecute pos d1 d2                      | Just l1 <- getImplementationLanguage d1            = [TyError pos d2 $ "Execute: The second diagram is not a valid execution unit for the language '" ++ l1 ++ "'."]
checkTyExecute pos d1 d2                      | Nothing <- getImplementationLanguage d1            = [TyError pos d1 $ "Execute: The first diagram is not executable."]

checkTyCompile :: SourcePos -> DiagType -> DiagType -> [Diagnostic]
checkTyCompile pos d1 (Type_Compiler (l2, _) _) | Just l1 <- getImplementationLanguage d1, l1 == l2  = [] -- Languages match!!
checkTyCompile pos d1 (Type_Compiler (l2, _) _) | Just l1 <- getImplementationLanguage d1, l1 /= l2  = [TyError pos d1 $ "Compile: The implementation language '" ++ l1 ++ "' of the first diagram doesn't match the compiler frontend language '" ++ l2 ++ "' of the second diagram." ]
checkTyCompile pos d1 d2                        | Just l1 <- getImplementationLanguage d1            = [TyError pos d2 $ "Execute: The second diagram is not a valid compiler for the language '" ++ l1 ++ "'."]
checkTyCompile pos d1 d2                        | Nothing <- getImplementationLanguage d1            = [TyError pos d1 $ "Execute: The first diagram is not compilable."]
}

{
replaceImplementationLanguage :: Implementation -> DiagType -> DiagType
replaceImplementationLanguage l1 (Type_Program     (Right _) ) = Type_Program     $ l1
replaceImplementationLanguage l1 (Type_Compiler l2 (Right _) ) = Type_Compiler l2 $ l1
replaceImplementationLanguage l1 (Type_Executor l2 (Right _) ) = Type_Executor l2 $ l1
replaceImplementationLanguage _   _                            = Type_Top
  
getImplementationLanguage :: DiagType -> Maybe Language
getImplementationLanguage (Type_Compiler _ (Right x)) = Just x
getImplementationLanguage (Type_Executor _ (Right x)) = Just x
getImplementationLanguage (Type_Program    (Right x)) = Just x
getImplementationLanguage _                           = Nothing

mkCompilerType :: Implementation -> Maybe SourceAndTargetLanguage -> DiagType
mkCompilerType m (Just l12) = Type_Compiler l12 m
mkCompilerType _ Nothing    = Type_Top

mkExecutorType :: Implementation -> Maybe Language -> DiagType
mkExecutorType l2 (Just l1) = Type_Executor l1 l2
mkExecutorType _  Nothing   = Type_Top

mkProgramType :: Implementation -> DiagType
mkProgramType m = Type_Program m
}

  
{
getTypeDiagnostics :: Diag -> [Diagnostic]
getTypeDiagnostics diag = typeDiagnostics_Syn_Diag (wrap_Diag (sem_Diag diag) Inh_Diag)
}
